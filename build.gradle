plugins {
    id "io.github.goooler.shadow" version "8.1.8"
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.0.0'
}

group = 'net.refractored'
version = '2.0'

repositories {
    mavenCentral()

    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = "https://repo.essentialsx.net/releases/"}
    maven { url = 'https://jitpack.io' }
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = 'https://repo.auxilor.io/repository/maven-public/' }
}

String getGitHash() {
    def gitCommitHash = "unknown"
    try {
        def workingDir = new File("${project.projectDir}")
        def result = 'git rev-parse --short HEAD'.execute(null, workingDir)
        result.waitFor()
        if (result.exitValue() == 0) {
            gitCommitHash = result.text.trim()
        }
    } catch (e) {
    }
    return gitCommitHash
}

String getCurrentGitBranch() {
    def gitBranch = "unknown"
    try {
        def workingDir = new File("${project.projectDir}")
        def result = 'git rev-parse --abbrev-ref HEAD'.execute(null, workingDir)
        result.waitFor()
        if (result.exitValue() == 0) {
            gitBranch = result.text.trim()
        }
    } catch (e) {
    }
    return gitBranch
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.20-R0.1-SNAPSHOT")

    compileOnly("com.willfp:eco:6.70.1")

    // EssentialsX
    compileOnly 'net.essentialsx:EssentialsX:2.20.1'

    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.6.3-beta-14'

    // Kotlin
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:2.0.0"

    // Lamp (Commands)
    implementation "com.github.Revxrsal.Lamp:common:3.2.1"
    implementation "com.github.Revxrsal.Lamp:bukkit:3.2.1"

    // Adventure (Components)
    compileOnly "net.kyori:adventure-platform-bukkit:4.1.2"
    implementation "net.kyori:adventure-text-minimessage:4.16.0"

    implementation("org.bstats:bstats-bukkit:3.0.2")

    // ORMLite (ORM)
    compileOnly "com.j256.ormlite:ormlite-core:5.6"
    compileOnly "com.j256.ormlite:ormlite-jdbc:5.6"

    // Vault (Economy)
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

    // SpiGUI (GUI)
    implementation 'com.samjakob:SpiGUI:1.3.1'

    compileOnly 'com.github.Emibergo02:RedisChat:5.3.1'

}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    if (getCurrentGitBranch() != "main") {
        version = version + "-" + getCurrentGitBranch() + "-" + getGitHash()
    }
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "21"
    kotlinOptions.javaParameters = true
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "21"
}

compileJava {
    options.compilerArgs += ["-parameters"]
}

shadowJar {
    archiveFileName = "JobListings-${version}.jar"
//    relocate('kotlin', 'net.refractored.libs.kotlin')
    relocate("org.bstats", "net.refractored.libs.bstats")


}

build.dependsOn shadowJar