plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.23'
}

group = 'net.refractored'
version = '1.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = 'https://jitpack.io' }
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = 'https://repo.auxilor.io/repository/maven-public/' }

    maven { url "https://repo.essentialsx.net/releases/"}
    maven { url "https://repo.auxilor.io/repository/maven-public/"}
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")

    compileOnly("com.willfp:eco:6.70.1")

    // EssentialsX
    compileOnly 'net.essentialsx:EssentialsX:2.19.0'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    // Lamp (Commands)
    implementation "com.github.Revxrsal.Lamp:common:3.1.9"
    implementation "com.github.Revxrsal.Lamp:bukkit:3.1.9"

    // Adventure (Components)
    compileOnly "net.kyori:adventure-platform-bukkit:4.1.2"
    implementation "net.kyori:adventure-text-minimessage:4.16.0"

    // ORMLite (ORM)
    implementation "com.j256.ormlite:ormlite-core:5.6"
    implementation "com.j256.ormlite:ormlite-jdbc:5.6"

    // Vault (Economy)
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

    // SpiGUI (GUI)
    implementation 'com.samjakob:SpiGUI:1.3.1'

}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "17"
    kotlinOptions.javaParameters = true
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "17"
}

compileJava {
    options.compilerArgs += ["-parameters"]
}


shadowJar {
    relocate('kotlin', 'net.refractored.libs.kotlin')
}

build.dependsOn shadowJar